#el chip va en la centrifugadora
alias centr db
#los leds peque√±os en los puertos
alias debug d0
alias debug2 d1

#se puede jugar con estos vlaores para mirar una mayor eficiciencia
define acel 10
define maxrpm 300
define temp 900

inicio:
#yield importante
yield
#ojo con el hash hay apiladores de un aldo y del otro StructureStackerReverse y StructureStacker
#leer por baches los apiladores elq ue se llama "apilador deeps". El slot 0. EL 2 del final es el minimo por si hay varios (nod eberia haber mas de uno)
lbns r0 HASH("StructureStackerReverse") HASH("apilador deeps") 1 Occupied 2
s debug Setting r0
#si esta ocupado la centrifugadra tiene carga apra rato apagamos los mineros, sino los encendemos
beqz r0 encenderMineros
sbn HASH("StructureTransformerSmall") HASH("deeps") On 0
#hagamos lo que hagamos hay que seguiior con le centrifugadora
centrifugadora:
#tinee que estar cerrada o se para
s centr Open 0
#leemos rpm y estress
l r0 centr Rpm
l r1 centr Stress
s debug Color 1
s debug Setting r1
#si llegamos al maximo RPm ya no subimos potencia
bgt r0 maxrpm procesa
#si no llegamos y no hay muhco estres acelera
blt r1 30 acelerar
#si hya muhco estres hay que hacer una parada de emergencia
bgt r1 90 reposar
#si llegue sin acelerar ni parar no aumento
s centr Throttle 0
j inicio

encenderMineros:
#enciendo un tranformador que alimenta los deeps miners
sbn HASH("StructureTransformerSmall") HASH("deeps") On 1
j centrifugadora

acelerar:
#esperar a que regularice
yield
#leo la injecccion actual de velocidad
l r0 centr Throttle
#le agrego la aceleracion, para meter mas
add r0 r0 acel
s centr Throttle r0
#espero a que haga efecto
sleep 5
j inicio

procesa:
#esperar a que regularice
yield
#no caliento
s centr CombustionLimiter 0
#carog temperatura
l r0 centr Temperature
#si la temperatura es baja caliento
blt r0 temp calenta
#si esta alto el estres parada de emergencia
l r2 centr Stress
bgt r2 50 reposar
#leo los elementos porcesados
l r1 centr Reagents
s debug2 Setting r1
s debug2 Color 3
#si ya se lleno de procesados hayq eu parar para vaciar
bgt r1 300 parar
#sino reinicia este ciclo
j procesa

calenta:
#abrir el mechero
s centr CombustionLimiter 1
j procesa

parar:
#parada total
yield
#veo si hay algo procesado dentro de la maquina, si ya se descargo todo reinicio
l r1 centr Reagents
s debug2 Setting r1
# no pongo 0 porque a veces quedan decimas de gramo de minerales, que no se pueden sacar. Hay que juntarlos con lo del proximo lote
blt r1 5 inicio
#abro la maquina y la paro totalmentes y le dejod e inyectar calor
s centr Open 1
s centr Throttle 0
s centr CombustionLimiter 0
j parar

reposar:
#parada de emergncia
yield
#apago todo
s centr Throttle 0
s centr CombustionLimiter 0
#solo reinicio si el estres es cero.
l r2 centr Stress
s debug Setting r2
s debug Color 4
beqz r2 inicio
j reposar
